buildscript {
    def liquibase_gradle_plugin = System.getProperty('VERSION_LIQUIBASE_GRADLE_PLUGIN') ?: VERSION_LIQUIBASE_GRADLE_PLUGIN
    def liquibase = System.getProperty('VERSION_ORG_LIQUIBASE') ?: VERSION_ORG_LIQUIBASE

    dependencies {
        classpath "gradle.plugin.com.avast.gradle:gradle-docker-compose-plugin:0.9.5"
        classpath("org.liquibase:liquibase-gradle-plugin:$liquibase_gradle_plugin") {
            exclude(module: 'liquibase-core') // exclude the dependency on liquibase-core:3.6.3
        }
        classpath("org.liquibase:liquibase-core:$liquibase")
        classpath('mysql:mysql-connector-java:8.0.19')
        classpath('org.postgresql:postgresql:42.2.13')
    }
}

plugins {
    id 'java'
    id 'org.liquibase.gradle' version '2.0.2'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

static def getEnv(key, defaultValue) {
    return System.getenv(key) ?: defaultValue
}

ext.libVers = [
        liquibase               : getEnv('VERSION_ORG_LIQUIBASE', VERSION_ORG_LIQUIBASE),
        liquibase_gradle_plugin : getEnv('VERSION_LIQUIBASE_GRADLE_PLUGIN', VERSION_LIQUIBASE_GRADLE_PLUGIN),
        liquibase_groovy_dsl    : getEnv('VERSION_LIQUIBASE_GROOVY_DSL', VERSION_LIQUIBASE_GROOVY_DSL),
        mysql_connector_java    : getEnv('VERSION_MYSQL_CONNECTOR_JAVA', VERSION_MYSQL_CONNECTOR_JAVA),
]

dependencies {
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-core', version: libVers.liquibase
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-gradle-plugin', version: libVers.liquibase_gradle_plugin
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-groovy-dsl', version: libVers.liquibase_groovy_dsl
    liquibaseRuntime group: 'mysql', name: 'mysql-connector-java', version: libVers.mysql_connector_java
    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.13'
    liquibaseRuntime group: 'ch.qos.logback', name: 'logback-core', version: libVers.logback
    liquibaseRuntime group: 'ch.qos.logback', name: 'logback-classic', version: libVers.logback
}

def mysql_db_password = getEnv('MYSQLDBPASS', DATABASE_PASSWORD)
def mysql_db_url = getEnv('MYSQLDBURL', DATABASE_URL)
def mysql_db_username = getEnv('MYSQLDBUSER', DATABASE_USERNAME)
def mysql_db_url_db = mysql_db_url + '?useUnicode=true&useJDBCCompliantTimezoneShift=true&useLegacyDatetimeCode=false&serverTimezone=UTC'

def pg0_db_password = getEnv('PGDBPASS0', PG0_DATABASE_PASSWORD)
def pg0_db_url = getEnv('PGDBURL0', PG0_DATABASE_URL)
def pg0_db_username = getEnv('PGDBUSER0', PG0_DATABASE_USERNAME)
def pg0_db_url_db = pg0_db_url

def pg1_db_password = getEnv('PGDBPASS1', PG1_DATABASE_PASSWORD)
def pg1_db_url = getEnv('PGDBURL1', PG1_DATABASE_URL)
def pg1_db_username = getEnv('PGDBUSER1', PG1_DATABASE_USERNAME)
def pg1_db_url_db = pg1_db_url

def run_list = project.properties['runList'] ?: 'mysql_db, pg0_db, pg1_db'

liquibase {
    activities {
        mysql_db {
            classpath "$projectDir/database/mysql/"
            changeLogFile "mysql-Change_Log.xml"
            url mysql_db_url_db
            username mysql_db_username
            password mysql_db_password
        }
        pg0_db {
            classpath "$projectDir/database/pg/"
            changeLogFile "pg-Change_Log.xml"
            url pg0_db_url_db
            username pg0_db_username
            password pg0_db_password
        }
        pg1_db {
            classpath "$projectDir/database/pg/"
            changeLogFile "pg-Change_Log.xml"
            url pg1_db_url_db
            username pg1_db_username
            password pg1_db_password
        }
    }
    runList = run_list
}
